using CSV
using DataFrames
using Accessors

function transform_floats(
    x,
    pr::Float64 = 100.0,
    eps::Float64 = 1e-9,
)
    if (x * pr - floor.(x) * pr) < eps
        return Int.(floor.(x * pr))
    else
        return Int.(ceil.(x * pr))
    end
end

function read_evrptw_instance(
    instance_name::String,
    n_vehicles::Int,
    travel_cost_coeff::Int,
    ;
    n_charging::Int = 0, # If 0, read from file
    n_customers::Int = 0, # If 0, read from file
    scale_time_horizon::Float64 = 1.0,
    scale_charge_capacity::Float64 = 1.0,
    scale_load_capacity::Float64 = 1.0,
    data_dir::String = "data/",
)
    # Read instance
    instance_fp = joinpath(data_dir, "evrptw/Instances/", instance_name * ".txt")
    instance_lines = readlines(instance_fp)
    sep = findfirst(x -> occursin(r"^\s*$", x), instance_lines)
    table_str = join(instance_lines[1:sep-1], "\n")
    df = CSV.read(
        IOBuffer(table_str), 
        DataFrame, 
        header=["StringID", "Type", "x", "y", "demand", "ReadyTime", "DueDate", "ServiceTime"],
        skipto=2,
        delim=' ', 
        ignorerepeated=true,
    )
    
    (
        B_, # Unscaled battery capacity
        C,  # Load capacity
        _,
        inverse_refueling_rate,  # Inverse battery refueling rate
        _
    ) = [parse(Float64, match(r"/([\d.]+)/", x)[1]) for x in instance_lines[sep+1:end]]

    # Scale load capacity
    C = Int(round(C * scale_load_capacity))

    # Scaled battery capacity
    B = transform_floats(B_ * scale_charge_capacity * inverse_refueling_rate)

    # DataFrames for depots, customers, charging stations
    Random.seed!(0)
    depots_df = filter(r -> r.Type == "d", df)
    customers_df = filter(r -> r.Type == "c", df)
    charging_df = filter(r -> r.Type == "f", df)

    if n_customers > 0
        customers_df = customers_df[1:n_customers, :]
    end
    if n_charging > 0
        charging_df = charging_df[1:n_charging, :]
    end

    # Index sets
    n_depots = nrow(depots_df)
    n_customers = nrow(customers_df)
    n_charging = nrow(charging_df)
    n_nodes = n_depots + n_customers + n_charging

    N_customers = 1:n_customers
    N_depots = n_customers+1:n_customers+n_depots
    N_vehicles = 1:n_vehicles
    N_charging = n_customers+n_depots+1:n_customers+n_depots+n_charging
    N_depots_charging = n_customers+1:n_customers+n_depots+n_charging
    N_nodes = 1:n_customers+n_depots+n_charging

    node_labels = merge(Dict(
        N_depots .=> depots_df[!, :StringID]
    ), Dict(
        N_customers .=> customers_df[!, :StringID]
    ), Dict(
        N_charging .=> charging_df[!, :StringID]
    ))

    # Locations 
    depot_coords = depots_df[:, [:x, :y]] |> Matrix{Float64} |> transpose
    customer_coords = customers_df[:, [:x, :y]] |> Matrix{Float64} |> transpose
    charging_coords = charging_df[:, [:x, :y]] |> Matrix{Float64} |> transpose

    coords = hcat(customer_coords, depot_coords, charging_coords)
    (xmin, xmax) = extrema(coords[1, :])
    (ymin, ymax) = extrema(coords[2, :])

    distances = Distances.pairwise(Euclidean(), coords; dims=2)
    
    # Starting locations of vehicles
    start_depots = StatsBase.sample(N_depots, n_vehicles, replace = true)
    V = Dict(i => findall(x -> x==i, start_depots) for i in N_depots)
    v_start = v_end = Dict(i => length(V[i]) for i in N_depots)

    # Travel cost
    c = transform_floats.(distances)
    # (Scaled) travel time
    travel_times = copy(distances)
    for i in 1:n_customers
        travel_times[i, :] .+= customers_df[i, "ServiceTime"]
    end
    t = transform_floats.(travel_times)
    # Battery consumption
    q = transform_floats.(distances * inverse_refueling_rate)

    # Demand
    d = vcat(
        customers_df[!, :demand],
        depots_df[!, :demand],
        charging_df[!, :demand],
    )

    # Time windows
    α = vcat(
        customers_df[!, :ReadyTime],
        depots_df[!, :ReadyTime],
        charging_df[!, :ReadyTime],
    )
    α = transform_floats.(α * scale_time_horizon)
    β = vcat(
        customers_df[!, :DueDate],
        depots_df[!, :DueDate],
        charging_df[!, :DueDate],
    )
    β = transform_floats.(β * scale_time_horizon)
    T = Int(round(maximum(β)))

    data = EVRPData(
        n_depots,
        n_customers,
        n_vehicles,
        n_charging,
        n_depots + n_charging,
        n_nodes,
        N_customers,
        N_depots,
        N_vehicles,
        N_charging,
        N_depots_charging,
        N_nodes,
        node_labels,
        "evrptw", # depot_pattern
        "evrptw", # customer_pattern
        "evrptw", # charging_pattern
        xmin, 
        xmax, 
        ymin,
        ymax, 
        customer_coords,
        depot_coords,
        charging_coords,
        coords,
        distances,
        V,
        v_start,
        v_end,
        c,
        t,
        q,
        d,
        C,
        T,
        α,
        β,
        inverse_refueling_rate,
        B,
        travel_cost_coeff,
    )

    return data
end

function stretch_time_windows!(
    data::EVRPData,
    stretch_factor::Float64,
)
    if !(0.0 <= stretch_factor <= 1.0)
        error("stretch_factor must be in [0, 1]")
    end
    data.α .= Int.(round.(
        # stretch_factor .* 0 .+ 
        (1 - stretch_factor) .* data.α 
    ))
    data.β .= Int.(round.(
        stretch_factor .* data.T
        .+ (1 - stretch_factor) .* data.β
    ))
    return
end

function remove_time_windows!(
    data::EVRPData,
    remove_factor::Float64,
    ;
    seed::Union{Int, Nothing} = nothing
)
    if !(0.0 <= remove_factor <= 1.0)
        error("remove_factor must be in [0, 1]")
    end
    n_TWs_remove = Int(round(remove_factor * data.n_customers * 2))
    
    if !isnothing(seed)
        Random.seed!(seed)
    end
    rand_inds = Random.shuffle(1:2*data.n_customers)[1:n_TWs_remove]

    for i in rand_inds
        if i <= data.n_customers
            j = i
            data.α[j] = 0
        else
            j = i - data.n_customers
            data.β[j] = data.T
        end
    end
    return
end

function duplicate_time_horizon!(
    data::EVRPData,
    batch_size::Int,
    ;
    seed::Int = 0,
)
    @assert data.n_customers % batch_size == 0

    Random.seed!(seed)

    shuffled_customers = shuffle(1:data.n_customers)
    n_batches = data.n_customers ÷ batch_size
    batches = [
        shuffled_customers[(k-1)*batch_size+1:k*batch_size] 
        for k in 1:n_batches
    ]
    for (k, batch) in enumerate(batches)
        data.α[batch] .+= (k-1) * data.T
        data.β[batch] .+= (k-1) * data.T
    end
    @reset data.T = data.T * n_batches
    data.β[data.N_depots_charging] .= data.T
    return data
end
